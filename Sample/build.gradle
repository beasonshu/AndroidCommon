/*
 * Copyright (C) 2015  Haiyang Yu Android Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
// 方法定义获取当前时间
def releaseTime() {
    return new Date().format("yyyy_MMdd")
}
// 方法定义获取当前时间
def generateVersionCode() {
    return new Date().format("yyyyMMddHH")
}

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        // 注意这里只是设置了appid的前缀 在buildTypes里面有设置后缀
        applicationId APPLICATION_ID_PREFIX
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode generateVersionCode() as int
        versionName VERSION_NAME

        // 配置 含有的版本类型
        buildConfigField "int", "VERSION_DEVELOP", VERSION_DEVELOP
        buildConfigField "int", "VERSION_QA", VERSION_QA
        buildConfigField "int", "VERSION_UAT", VERSION_UAT
        buildConfigField "int", "VERSION_OFFICIAL", VERSION_OFFICIAL
        // 配置默认版本-为开发版本
        buildConfigField "int", "VERSION_TYPE", VERSION_DEVELOP
        // 配置语言信息
        //resConfig "zh,zh-rHK,zh-rTW,en,xhdpi"
    }
    //签名
    signingConfigs {
        release {
            storeFile file(RELEASE_STOREFILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    productFlavors {

        //  开发服务器相关配置
        develop {
            // AndroidManifest 中配置的文件
            manifestPlaceholders = [BAIDU_STATISTICS_CHANNEL: DEVELOP_BAIDU_STATISTICS_CHANNEL]
            // 设置那个版本
            buildConfigField "int", "VERSION_TYPE", VERSION_DEVELOP
        }

        // QA 测试服务器相关配置
        qa {
            // AndroidManifest 中配置的文件
            manifestPlaceholders = [BAIDU_STATISTICS_CHANNEL: QA_BAIDU_STATISTICS_CHANNEL]

            // 设置那个版本
            buildConfigField "int", "VERSION_TYPE", VERSION_QA
        }

        // 客户UAT服务器相关配置
        uat {
            // AndroidManifest 中配置的文件
            manifestPlaceholders = [BAIDU_STATISTICS_CHANNEL: UAT_BAIDU_STATISTICS_CHANNEL]
            // 设置那个版本
            buildConfigField "int", "VERSION_TYPE", VERSION_UAT
            // UAT版本号
            versionName UAT_VERSION_NAME
        }

        // 正式服务器相关配置
        official {
            // AndroidManifest 中配置的文件
            manifestPlaceholders = [BAIDU_STATISTICS_CHANNEL: RELEASE_BAIDU_STATISTICS_CHANNEL]
            // 设置那个版本
            buildConfigField "int", "VERSION_TYPE", VERSION_OFFICIAL
            // 正式服务器的版本号
            versionName OFFICIAL_VERSION_NAME
        }

    }

    buildTypes {
        // 单纯的补充默认编译的属性
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationIdSuffix TEST_APPLICATION_ID_SUFFIX
            // AndroidManifest 中配置的文件
            manifestPlaceholders = [
                    // App的名字
                    APP_NAME           : TEST_APP_NAME,
                    // 百度统计的ID
                    BAIDU_STATISTICS_ID: TEST_BAIDU_STATISTICS_ID,
            ]
        }


        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationIdSuffix RELEASE_APPLICATION_ID_SUFFIX
            manifestPlaceholders = [
                    // App的名字
                    APP_NAME           : RELEASE_APP_NAME,
                    // 百度统计的ID
                    BAIDU_STATISTICS_ID: RELEASE_BAIDU_STATISTICS_ID,
            ]

            //将release版本的包名重命名，加上版本及日期
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    def versionName = variant.productFlavors[0].versionName;
                    if (versionName == null) {
                        versionName = defaultConfig.versionName;
                    }
                    if (outputFile != null && outputFile.name.endsWith('release.apk')) {
                        def fileName = "Common_${variant.productFlavors[0].name}_v${versionName}_${releaseTime()}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    dexOptions {
        jumboMode true
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
        // Move the tests to tests/java, tests/res, etc...
        //  instrumentTest.setRoot('tests')
        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
        packagingOptions {
            exclude 'META-INF/DEPENDENCIES.txt'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/notice.txt'
            exclude 'META-INF/license.txt'
            exclude 'META-INF/dependencies.txt'
            exclude 'META-INF/LGPL2.1'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
    }
}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // 基础库
    compile project(':BrightCommon')
}

